plugins {
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.3'
    id 'com.gradleup.shadow' version '9.3.0'
    id 'maven-publish'
}

ext {
    userHome = System.getProperty("user.home")
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(java_version)
    withSourcesJar()
    withJavadocJar()
}

// Quiet warnings about missing Javadocs.
javadoc {
    options.addStringOption('Xdoclint:-missing', '-quiet')
}

configurations {
    create("shade")
    implementation {
        extendsFrom(configurations["shade"])
    }
}

repositories {
    mavenCentral()
}

// Adds the Hytale server as a build dependency, allowing you to reference and
// compile against their code. This requires you to have Hytale installed using
// the official launcher for now.
dependencies {
    implementation(files("$userHome/AppData/Roaming/Hytale/install/release/package/game/latest/Server/HytaleServer.jar"))
    shade "org.ow2.asm:asm:${project.asm_version}"
    shade "org.ow2.asm:asm-analysis:${project.asm_version}"
    shade "org.ow2.asm:asm-commons:${project.asm_version}"
    shade "org.ow2.asm:asm-tree:${project.asm_version}"
    shade "org.ow2.asm:asm-util:${project.asm_version}"
    shade "net.fabricmc:sponge-mixin:${project.mixin_version}"
    shade 'org.ow2.sat4j:org.ow2.sat4j.core:2.3.6'
    shade 'org.ow2.sat4j:org.ow2.sat4j.pb:2.3.6'
    shade "io.github.llamalad7:mixinextras-fabric:${project.mixin_extras_version}"
}

// Determines which properties will be passed from the Gradle runtime to
// resources as they are compiled into your plugin.
processResources {
    var expandProps = [
            'name'          : project.name,
            'version'       : project.version,
            'group'         : project.group,
            'description'   : project.description,
            'website'       : project.website,
            'server_version': project.server_version,
            'project_group' : project.plugin_group,
            'author'        : project.author
    ]
    filesMatching(['manifest.json']) {
        expand expandProps
    }
    inputs.properties(expandProps)
}

// Create the working directory to run the server if it does not already exist.
def serverRunDir = file("$projectDir/run/server")
if (!serverRunDir.exists()) {
    serverRunDir.mkdirs()
}

// Creates a run configuration in IDEA that will run the Hytale server with
// your plugin and the default assets.
idea.project.settings.runConfigurations {
    'HytaleServer'(org.jetbrains.gradle.ext.Application) {
        mainClass = 'com.hypixel.hytale.Main'
        moduleName = project.idea.module.name + '.main'
        programParameters = "--allow-op --accept-early-plugins --assets=$userHome/AppData/Roaming/Hytale/install/release/package/game/latest/Assets --packs=$userHome/AppData/Roaming/Hytale/UserData/Packs"
        workingDirectory = serverRunDir.absolutePath
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.name
            version = project.version
        }
    }
    repositories {
        mavenLocal()
    }
}

tasks.shadowJar {
    configurations = [project.configurations.shade]
    exclude 'about.html', 'about.html', 'fabric.mod.json', 'LICENSE.txt', 'LICENSE_MixinExtras', 'sat4j.version'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/services/cpw.**', 'META-INF/services/org.spongepowered.tools.obfuscation.service.IObfuscationService'
    exclude 'com/google/gson/**', 'org/sat4j/**'
    mergeServiceFiles()
}